buildscript {
   ext {
      springVersion = '5.1.8.RELEASE'
      swaggerVersion = "2.9.2"
      jacksonVersion = '2.10.3'
      jsonPathVersion = '2.4.0'
      httpClientVersion = '4.5.12'
      logbackVersion = '1.2.3'
      jupiterVersion = '5.6.2'
      jfUtilsVersion = '0.1.1'
   }
}

plugins {
   id 'java'
   id 'maven-publish'
   id 'signing'
}

group 'club.javafamily'
version '0.1.1.RELEASE'
//version '0.1.1-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
   jcenter()
   mavenLocal()
   mavenCentral()
}

dependencies {
   // jackson
   implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
   implementation("com.jayway.jsonpath:json-path:${jsonPathVersion}") {
      exclude(group: 'org.slf4j', module: 'slf4j-api')
   }

   // httpclient
   implementation("org.apache.httpcomponents:httpclient:${httpClientVersion}") {
      exclude(group: 'commons-logging', module: 'commons-logging')
   }

   // springfox swagger
   implementation "io.springfox:springfox-swagger2:${swaggerVersion}"
//   implementation "io.springfox:springfox-swagger-ui:${swaggerVersion}"

   // utils
   implementation "club.javafamily:javafamily-utils:${jfUtilsVersion}"

   // logging
   implementation "ch.qos.logback:logback-classic:${logbackVersion}"

   // test
   testImplementation "org.junit.jupiter:junit-jupiter:${jupiterVersion}"
}

task sourcesJar(type: Jar) {
   from sourceSets.main.allSource
   archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
   from javadoc
   archiveClassifier = 'javadoc'
}

publishing {
   // 定义发布什么
   publications {
      mavenJava(MavenPublication) {
         groupId = project.group
         artifactId = project.name
         version = "${version}"
         from components.java
         artifact sourcesJar
         artifact javadocJar
         pom {
            // 构件名称
            // 区别于artifactId，可以理解为artifactName
            name = 'JavaFamily Rest Api Interface'
            // 构件描述
            description = 'JavaFamily Rest Api library'
            // 构件主页
            url = 'https://github.com/JavaFamilyClub/javafamily-rest'
            // 许可证名称和地址
            licenses {
               license {
                  name = 'The Apache License, Version 2.0'
                  url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
               }
            }
            // 开发者信息
            developers {
               developer {
                  id = 'JavaFamilyClub'
                  name = 'JavaFamily'
                  email = 'javafamily.club@outlook.com'
                  organization = 'JavaFamily'
               }
            }
            scm {
               url = 'https://github.com/JavaFamilyClub/javafamily-rest'
               connection = 'scm:git:git://github.com/JavaFamilyClub/javafamily-rest.git'
               developerConnection = 'scm:git:ssh://git@github.com/JavaFamilyClub/javafamily-rest.git'
            }
         }
      }
   }
   repositories {
      maven {
         def snapshotsUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
         def releasesUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'

         url = project.version.endsWith('-SNAPSHOT') ? snapshotsUrl : releasesUrl
         credentials {
            username sonatypeUsername
            password sonatypePassword
         }
      }
   }
}

signing {
   sign publishing.publications.mavenJava
}

javadoc {
   options.addStringOption("charset", "UTF-8")
}

test {
   useJUnitPlatform()
   testLogging {
      events "passed", "skipped", "failed"
   }
}

wrapper {
   gradleVersion '6.2.1'
}
